set term postscript enhanced portrait solid color  
set output 'CODATABootstrapComparison.eps'

fn = 'codata.dat'
CDfn = 'CODATA2010Recommendation.dat'
BSfn = 'BootstrapResult.dat'
BRfn = 'BirgeRatioedTraditionalAverage.dat'

set key noauto

set title 'Comparing CODATA 2010, Birge-ratio weighted average, and Bootstrapping'

set multiplot layout 2,1

set xlabel '( G / (CODATA2010 G) - 1 ) * 1e6'

set key noauto

CD2010 = 6.67384

unset ytics

set label 1 'CODATA 2010' 		 at 200,-2
set label 2 'Birge-ratio scaled' at 200,-4
set label 3 'Bootstrapped' 		 at 200,-6

set lmargin 5

pl [][-7:12]\
	fn   u (($4/CD2010-1)*1e6):0:($5/CD2010*1e6) w xerr lt 1 lw 2 lc -1 tit 'Experiments',\
	CDfn u (($4/CD2010-1)*1e6):(-2):($5/CD2010*1e6) w xerr lt 1 lw 2 lc 3  tit 'Aggregated',\
	BRfn u (($1/CD2010-1)*1e6):(-4):($2/CD2010*1e6) w xerr lt 1 lw 2 lc 3  ,\
	BSfn u (($1/CD2010-1)*1e6):(-6):($2/CD2010*1e6) w xerr lt 1 lw 2 lc 3  ,\
	-1 lt 0

set ytics
unset ylabel
unset label 1
unset label 2
unset label 3

set title 'G values bootstrapped from experiments'

pl [-400:400]\
	'BootstrapHistogram.dat' u (($1/CD2010 - 1) * 1e6):2 w boxes lt 3 
unset multiplot
